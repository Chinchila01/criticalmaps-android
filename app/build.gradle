apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'net.ltgt.errorprone'

// Place actual signing configuration in "keystore.properties"
// "keystore.properties" is in .gitignore and will not be checked into repo
def keystorePropertiesFile = rootProject.file("keystore.properties")
if(!keystorePropertiesFile.exists() ) {
    keystorePropertiesFile = rootProject.file("dummy_keystore.properties")
}
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "de.stephanlindauer.criticalmaps"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 34
        versionName "2.4.2"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        'butterknife.minSdk': getMinSdkVersion().getApiString()
                ]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
    }

    lintOptions {
        warning 'MissingTranslation'
        warning 'StringFormatInvalid'
        warning 'ExpiringTargetSdkVersion'

        // https://github.com/square/okio/issues/58
        // http://jakewharton.github.io/butterknife/
        warning 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.squareup:otto:1.3.8'
    implementation 'org.osmdroid:osmdroid-android:6.0.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.squareup.okhttp3:okhttp:3.12.0'
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    implementation 'org.ligi:AXT:0.37'
    implementation 'info.metadude.android:typed-preferences:2.1.0'
    implementation "com.google.dagger:dagger:$dagger_version"

    compileOnly 'javax.annotation:jsr250-api:1.0'
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    errorprone("com.google.errorprone:error_prone_core:2.3.1")

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.41'
    testImplementation 'androidx.annotation:annotation:1.0.0'
    testImplementation 'org.mockito:mockito-core:2.19.0'
}
